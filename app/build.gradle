plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk 32

    defaultConfig {
        applicationId "br.com.luciano.movieslist"
        minSdk 19
        targetSdk 32
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    ndkVersion "21.3.6528147"

    buildTypes {
        debug {
            //debuggable true
            //minifyEnabled true
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled true
        }
        homolog {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "main"

    productFlavors {
        main {
            applicationId "br.com.luciano.movieslist"
            dimension "main"
            buildConfigField 'String', 'CONSUMER_KEY', apikeyProperties['MOVIES_DB_API_KEY']
            buildConfigField 'String', 'CONSUMER_API', apikeyProperties['MOVIES_DB_API']
            buildConfigField 'String', 'IMAGE_URL', apikeyProperties['IMAGE_DB_API']
            buildConfigField 'boolean', 'SHOW_ACCESSIBILITY_MENU', 'true'
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.vectordrawable:vectordrawable:$vectordrawable_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"

    implementation "com.google.android.material:material:$material_version"

    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    implementation ('com.google.firebase:firebase-analytics') {
        exclude module: "play-services-ads-identifier"
    }
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"

    testImplementation "androidx.test:core:$androidTest_core_version"
    testImplementation "androidx.test.ext:junit:$androidTest_androidx_junit_version"
    testImplementation "junit:junit:$androidTest_junit_version"
    testImplementation "org.robolectric:robolectric:$androidTest_robolectric_version"
    testImplementation "androidx.arch.core:core-testing:$androidTest_core_testing_version"
    androidTestImplementation "androidx.test.ext:junit:$androidTest_androidx_junit_version"
}