plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk 32

    defaultConfig {
        applicationId "br.com.luciano.movieslist"
        minSdk 19
        targetSdk 32
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    ndkVersion "21.3.6528147"

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled true
        }
        homolog {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "main"

    productFlavors {
        main {
            applicationId "br.com.luciano.movieslist"
            dimension "main"
            buildConfigField 'String', 'CONSUMER_KEY', apikeyProperties['MOVIES_DB_API_KEY']
            buildConfigField 'String', 'CONSUMER_API', apikeyProperties['MOVIES_DB_API']
            buildConfigField 'String', 'IMAGE_URL', apikeyProperties['IMAGE_DB_API']
            buildConfigField 'boolean', 'SHOW_ACCESSIBILITY_MENU', 'true'
        }
    }
}

dependencies {
    def appDependencies = rootProject.ext.mainApplication
    def unitTestDependencies = rootProject.ext.unitTesting

    implementation appDependencies.appcompat
    implementation appDependencies.constraintlayout
    implementation appDependencies.vectordrawable

    implementation appDependencies.lifecycleLivedataKtx
    implementation appDependencies.lifecycleViewmodelKtx

    implementation appDependencies.navigationFragment
    implementation appDependencies.navigationUi

    implementation appDependencies.material

    implementation platform(appDependencies.firebaseBom)
    implementation (appDependencies.firebaseAnalytics) {
        exclude module: "play-services-ads-identifier"
    }
    implementation appDependencies.firebasePerf
    implementation appDependencies.firebaseCrashlytics

    implementation appDependencies.gson

    implementation appDependencies.glide
    annotationProcessor appDependencies.compiler

    implementation appDependencies.retrofit
    implementation appDependencies.converterGson

    implementation appDependencies.roomRuntime
    annotationProcessor appDependencies.roomCompiler
    implementation appDependencies.roomRxjava3

    testImplementation unitTestDependencies.androidxTestCore
    testImplementation unitTestDependencies.androidxTestCoreJunit
    testImplementation unitTestDependencies.junit
    testImplementation unitTestDependencies.robolectric
    testImplementation unitTestDependencies.coreTesting

    androidTestImplementation unitTestDependencies.androidxTestCoreJunit
}